dnl Process this file with autoconf to produce a configure script.
dnl ====================================================================
dnl
dnl     configure.in
dnl
dnl     libcdl.a
dnl
dnl ====================================================================
dnl####COPYRIGHTBEGIN####
dnl                                                                         
dnl ----------------------------------------------------------------------------
dnl Copyright (C) 1998, 1999, 2000 Red Hat, Inc.
dnl
dnl This file is part of the eCos host tools.
dnl
dnl This program is free software; you can redistribute it and/or modify it 
dnl under the terms of the GNU General Public License as published by the Free 
dnl Software Foundation; either version 2 of the License, or (at your option) 
dnl any later version.
dnl 
dnl This program is distributed in the hope that it will be useful, but WITHOUT 
dnl ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or 
dnl FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for 
dnl more details.
dnl 
dnl You should have received a copy of the GNU General Public License along with
dnl this program; if not, write to the Free Software Foundation, Inc., 
dnl 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
dnl
dnl ----------------------------------------------------------------------------
dnl                                                                          
dnl####COPYRIGHTEND####
dnl ====================================================================
dnl#####DESCRIPTIONBEGIN####
dnl
dnl Author(s):	bartv
dnl Contact(s):	bartv
dnl Date:	1998/12/16
dnl Version:	0.01
dnl
dnl####DESCRIPTIONEND####
dnl ====================================================================

dnl The core header file had better be present.
AC_INIT(cdlcore.hxx)

dnl Pick up the support files from the top-level of the source tree,
dnl one level above packages.
AC_CONFIG_AUX_DIR(..)

dnl Other standard options.
AC_CANONICAL_HOST

AM_INIT_AUTOMAKE(libcdl, 0.2)
CYG_AC_SUBST_VERSION(CYGNUM_LIBCDL_VERSION)
AM_MAINTAINER_MODE

dnl The package contains a library implemented in C++. It has to be
dnl buildable under NT. Only a static version of the library is
dnl of interest at the moment.
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AC_OBJEXT
AC_EXEEXT
CYG_AC_PROG_MSVC
CYG_AC_PROG_STANDARD_COMPILER_FLAGS

dnl AM_DISABLE_SHARED
dnl AM_PROG_LIBTOOL

dnl This package cannot be cross-compiled yet.
if test ${cross_compiling} = yes; then
  AC_MSG_ERROR(cross-compilation of this package is not supported)
fi

dnl Allow the usual assertion and trace related options.
CYG_AC_ARG_INFRASTRUCTURE

dnl What version of Tcl should be used?
CYG_AC_PATH_TCL

dnl ------------------------------------------------------------------------
dnl Where should the infrastructure headers come from?
dnl The default is ../../../../infra/current/host. If the
dnl appropriate header file is not found there, try
dnl $(DESTDIR)$(includedir). In addition allow the user to override
dnl the location with a suitable --with command line argument.

AC_MSG_CHECKING(for infrastructure files)
AC_ARG_WITH(infra,[ --with-infra=dir         location of the infrastructure build tree],[
  cyg_ac_infra_incdir=${with_infra}
  cyg_ac_infra_libdir=${with_infra}
],[
  if test -d ../infra ; then
    cyg_ac_infra_incdir=../infra
    cyg_ac_infra_libdir=../infra
  else
    cyg_ac_infra_incdir=${prefix}/include
    cyg_ac_ac_infra_libdir=${prefix}/lib
  fi
])
CYG_AC_MSVC_PATH(cyg_ac_infra_incdir)
CYG_AC_MSVC_PATH(cyg_ac_infra_libdir)
AC_SUBST(cyg_ac_infra_incdir)
AC_SUBST(cyg_ac_infra_libdir)
AC_MSG_RESULT(-I${cyg_ac_infra_incdir} -L${cyg_ac_infra_libdir})

dnl ------------------------------------------------------------------------
dnl If VC++ is being used then srcdir is likely to be a cygwin path
dnl rather than a Windows path. This means that the default -I
dnl paths generated by automake will not be acceptable to the compiler.
dnl This should probably be fixed in automake's comp-vars.am file,
dnl but for now this suffices. It is also necessary to worry about
dnl infra_incdir and tcl_incdir
if test "${MSVC}" = "yes" ; then
  INCLUDES="\"-I`cygpath -w ${srcdir} | tr \\\\\\\\ /`\" \"-I${cyg_ac_infra_incdir}\" \"-I${cyg_ac_tcl_incdir}\""
else
  INCLUDES="-I${cyg_ac_infra_incdir} -I${cyg_ac_tcl_incdir}"
fi
AC_SUBST(INCLUDES)

dnl --------------------------------------------------------------------
AM_CONFIG_HEADER(cdlconfig.h:config.h.in)
AC_OUTPUT(Makefile:Makefile.in testsuite/Makefile:testsuite/Makefile.in)
